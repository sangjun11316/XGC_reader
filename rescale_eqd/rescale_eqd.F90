!*****************
! Main program
! first creaded : 10/9/2023
! reading eqd file and scale B & R
! keeping q-profile the same
! write out to a new file
!********************

Module eq_module    
    character (LEN=256) :: eq_infilename, eq_outfilename
    character (len=80) eq_header, eq_tail
    real (8) :: eq_r_factor, eq_b_factor
    integer :: eq_mr, eq_mz, eq_mpsi
    real (8) :: eq_min_r, eq_max_r, eq_min_z, eq_max_z
    real (8) :: eq_axis_r, eq_axis_z, eq_axis_b
    real (8) :: eq_x_psi, eq_x_r, eq_x_z
    
    real (8), allocatable :: eq_psi_grid(:), eq_I(:), eq_psi_rz(:,:)
    
    ! limiter and boundary
    integer :: eq_nlim, eq_nbdry
    real (8), allocatable :: eq_rlim(:), eq_zlim(:), eq_rbdry(:), eq_zbdry(:)

    !logical :: eqd_revert_z
    !logical :: eqd_invert_psi
    !real (8) :: eqd_psi_factor = 1D0
    !real (8) :: eqd_I_factor = 1D0 
end module eq_module

program rescale_eqd
    implicit none

    print *, 'setup...'
    call setup
    
    print *, 'reading eqd...'
    call read_eqd

    print *, 'rescale...'
    call rescale

    print *, 'output eqd...'
    call output_eq
  
end program

subroutine rescale
    use eq_module
    implicit none
    real (8) :: lfac, bfac, pfac

    lfac=eq_r_factor
    bfac=eq_b_factor
    pfac=bfac * lfac * lfac ! psi

    
    call fac(eq_min_r,lfac)
    call fac(eq_max_r,lfac)
    call fac(eq_min_z,lfac)
    call fac(eq_max_z,lfac)

    call fac(eq_axis_r,lfac)
    call fac(eq_axis_z,lfac)
    call fac(eq_axis_b,bfac)

    call fac(eq_x_psi,pfac)
    call fac(eq_x_r,lfac)
    call fac(eq_x_z,lfac)

    eq_psi_grid=eq_psi_grid * pfac
    eq_I = eq_I * lfac * bfac
    eq_psi_rz=eq_psi_rz * pfac

contains
    subroutine fac(var, factor)
        real(8) :: var, factor
        var=var*factor
    end subroutine
end subroutine

subroutine setup
    use eq_module
    implicit none
    namelist /eq_param/  eq_infilename, eq_r_factor, eq_b_factor, eq_outfilename

    open(unit=14,file='input',action='read')
    read(14,nml=eq_param)
    close(14)

end subroutine

subroutine output_eq
    use eq_module
    implicit none
    integer :: i,j
    
    open(11, file=eq_outfilename, status='replace')
    write(11,300) 'Equilibrium data generated by rescale_eqd from eqd_filename'
    write(11,200) eq_mr, eq_mz, eq_mpsi
    write(11,100) eq_min_r, eq_max_r, eq_min_z, eq_max_z
    write(11,100) eq_axis_r, eq_axis_z,eq_axis_b 
    write(11,100) eq_x_psi, eq_x_r, eq_x_z
  
    !let axis psi=0
    write(11,100) (eq_psi_grid(i), i=1, eq_mpsi)
    write(11,100) (eq_I(i), i=1, eq_mpsi)
    write(11,100) ((eq_psi_rz(i,j) , i=1, eq_mr),j=1, eq_mz)
    write(11,200) -1
  
    ! limiter data
    write(11,200) eq_nlim
    write(11,5003) (eq_rlim(i),eq_zlim(i),i=1,eq_nlim)
    write(11,200) -1
    write(11,200) eq_nbdry
    write(11,5003) (eq_rbdry(i),eq_zbdry(i),i=1,eq_nbdry)
    write(11,200) -1
    write(11,300) eq_tail
    write(11,300) eq_infilename
    
    close(11)
  
100  format(4(e19.13,1x))
200  format(8I8)
300  format(a80)
5003 format(e19.13,' ',e19.13)
2000 format (6a8)
  
end subroutine output_eq


subroutine read_eqd
    use eq_module
    implicit none
    integer :: i, j, end_flag

    open(9,file=eq_infilename, status='old')
    read(9,300) eq_header
    read(9,200) eq_mr, eq_mz, eq_mpsi
  
    allocate(eq_psi_grid(eq_mpsi))
    allocate(eq_I(eq_mpsi), eq_psi_rz(eq_mr,eq_mz))
  
    read(9,100) eq_min_r, eq_max_r, eq_min_z, eq_max_z
    !print *,'min_r, max_r, min_z, max_z', eq_min_r,eq_max_r, eq_min_z, eq_max_z
    read(9,100) eq_axis_r, eq_axis_z,eq_axis_b  ! format changed 2006/02/24 - axis_z added
    !print *,'axis_r, axis_b', eq_axis_r, eq_axis_b
    read(9,100) eq_x_psi, eq_x_r, eq_x_z
    !print *, 'x_psi, x_r, x_z', eq_x_psi, eq_x_r, eq_x_z
    read(9,100) (eq_psi_grid(i), i=1, eq_mpsi)
    !print *, 'psi_grid', eq_psi_grid
    ! do i=1, eq_mpsi
    !  write(112,*) i, eq_psi_grid(i)
    !enddo
    !close(112)
    
    read(9,100) (eq_I(i), i=1,eq_mpsi)
    read(9,100) ((eq_psi_rz(i,j) , i=1, eq_mr),j=1, eq_mz)
    read(9,200) end_flag
    print *, 'eq end_flag', end_flag
    if(end_flag/=-1) then
        !additional eq. input
        print *, 'wrong file format'
        stop
    endif

    !following might not be used. 
    read(9,1101) eq_nlim
    allocate(eq_rlim(eq_nlim),eq_zlim(eq_nlim))

    do i=1, eq_nlim
        read(9,1102) eq_rlim(i),eq_zlim(i)
    enddo
    read(9,200) end_flag
    if(end_flag/=-1) print *, 'wrong file format after limiter'

    read(9,1101) eq_nbdry
    allocate(eq_rbdry(eq_nbdry),eq_zbdry(eq_nbdry))

    do i=1, eq_nbdry
        read(9,1102) eq_rbdry(i),eq_zbdry(i)
    enddo
    read(9,200) end_flag
    if(end_flag/=-1) print *, 'wrong file format after boundary'
    read(9,300) eq_tail

    print *, 'eq_axis_r=',eq_axis_r, 'target axis_r',eq_axis_r*eq_r_factor
    print *, 'eq_axis_b=',eq_axis_b, 'target axis_z',eq_axis_b*eq_b_factor
    

100  format(4(e19.13,1x))
200  format(8I8)
300  format(a80)
1101 format (i8)
1102 format (e19.13,1x,e19.13)
end subroutine  


